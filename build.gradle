// common build configuration for Randoop and agents
allprojects {

  // this project and agent subprojects are Java projects
  apply plugin: 'java'

  // use Maven Central when have external dependencies
  repositories {
    mavenCentral()
  }

  // Randoop version number - added to generated jar files
  version = '2.1.4'
}

//
// configuration specific to Randoop
//

description = "Randoop automated test generation"

sourceSets {
  systemtest {
    java {
      srcDir 'src/systemtest/java'
    }
    resources {
      srcDir 'src/systemtest/resources'
    }
  }
}

dependencies {
  // Randoop:
  //   uses local copy of Plume
  compile files('lib/plume.jar')

  // test:
  //   dependencies implicitly defined for sourceSet test
  //   uses JUnit
  testCompile group: 'junit', name: 'junit', version: '4.+'
  //   some tests use code from systemtests
  testCompile sourceSets.systemtest.output

  // systemtests:
  //   depends on output of main
  systemtestCompile sourceSets.main.output
  //   src/systemtest/java/ps1/RatPolyTest.java uses JUnit
  systemtestCompile group: 'junit', name: 'junit', version: '4.+'
}

// Compilation
sourceCompatibility = 1.7
compileJava.options.compilerArgs = ['-Xlint','-g','-Xlint:-classfile']
compileTestJava.options.compilerArgs = ['-nowarn','-g','-Xlint:-classfile']
compileSystemtestJava.options.compilerArgs = ['-nowarn','-g','-Xlint:-classfile']

// Only want the jar file to include class files from main source set
jar {
  from sourceSets.main.output
}

javadoc {
  source sourceSets.main.allJava
}

test {
  // exclude exercised-class tests b/c require javaagent
  exclude 'randoop/instrument/**'

  // set working directory for JUnit tests to the resources directory instead of project
  workingDir = sourceSets.test.output.resourcesDir

  testLogging {
    showStandardStreams = true
    exceptionFormat = 'full'
  }

  reports {
    html.enabled = true
  }

  // excluding problematic tests during Gradle setup
  // unless otherwise noted, these are tests that were not previously run by Makefile
  exclude 'randoop/test/NonterminatingInputTest.*'
  exclude 'randoop/test/EmptyTest.*'
  exclude 'randoop/test/RandoopPerformanceTest.*' // had target but not run
  exclude 'randoop/test/ForwardExplorerPerformanceTest.*'
  exclude 'randoop/test/Test_SomeDuplicates.*'
  exclude 'randoop/test/Test_SomePass.*'
  exclude 'randoop/operation/OperationParserTests.*'
}

// want to archive locally
uploadArchives {
  repositories {
    flatDir {
      dirs 'dist'
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}
